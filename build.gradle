buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.7.1'
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'groovy'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply from: 'gradle/dist.gradle'

group = 'org.mnode.ical4j'
description = '''
Support for the vCard specification
'''

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    // sonatype snapshots
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'org.mnode.ical4j:ical4j:2.0.2',
            'commons-io:commons-io:2.3', 'org.codehaus.groovy:groovy-all:2.3.6',
            'org.slf4j:jcl-over-slf4j:1.7.14'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

jar {
    from(project.rootDir) {
        include "LICENSE.txt"
    }
    
    manifest {
        instruction 'Require-Capability', '''osgi.extender;filter:="(osgi.extender=osgi.serviceloader.registrar)",
            osgi.extender;filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;filter:="(osgi.serviceloader=net.fortuna.ical4j.vcard.*)";cardinality:=multiple
        '''
        instruction 'Provide-Capability', '''osgi.serviceloader;osgi.serviceloader="net.fortuna.ical4j.vcard.ParameterFactory",
            osgi.serviceloader="net.fortuna.ical4j.vcard.PropertyFactory"
        '''
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

scmVersion {
    tag {
        prefix = 'ical4j-vcard'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
        'master': 'simple'
    ]
}

version = scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'http://ical4j.github.io'

                scm {
                    url 'https://github.com/ical4j/ical4j-vcard'
                    connection 'scm:git@github.com:ical4j/ical4j-vcard.git'
                    developerConnection 'scm:git@github.com:ical4j/ical4j-vcard.git'
                }

                licenses {
                    license {
                        name 'iCal4j - License'
                        url 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'fortuna'
                        name 'Ben Fortuna'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenArtifacts(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-vcard')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-vcard.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-vcard.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j-vcard/master/LICENSE.txt')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_key
    pkg {
        repo = 'maven'
        name = 'ical4j-vcard'
        userOrg = 'ical4j'
        licenses = ['BSD']
        vcsUrl = 'https://github.com/ical4j/ical4j-vcard.git'
        version {
            name = scmVersion.version
            desc = "iCal4j vCard $scmVersion.version"
            released  = new Date()
            vcsTag = "ical4j-vcard-$scmVersion.version"
            gpg {
                sign = isReleaseVersion
            }
            mavenCentralSync {
                sync = isReleaseVersion
                user = sonatype_username //OSS user token: mandatory
                password = sonatype_password //OSS user password: mandatory
                close = '0' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
//    configurations = ['archives']
    publications = ['mavenArtifacts']
}