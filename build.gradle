plugins {
    id 'java'
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
    id "pl.allegro.tech.build.axion-release" version "1.13.6"
    id "com.palantir.revapi" version "1.7.0"
    id "net.ltgt.errorprone" version "2.0.2" apply false
    id "biz.aQute.bnd.builder" version "$bndVersion"
    id 'org.javamodularity.moduleplugin' version '1.7.0' apply false
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    registerFeature('groovyDsl') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    api "org.mnode.ical4j:ical4j:$ical4jVersion",
            "commons-io:commons-io:$commonsIoVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    groovyDslImplementation "org.codehaus.groovy:groovy:$groovyVersion"

    // junit
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'

    // groovy-test
    testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"

    // spock
    testImplementation platform("org.spockframework:spock-bom:$spockVersion"),
            "org.spockframework:spock-core",
            "org.slf4j:slf4j-log4j12:$slf4jVersion",
            "org.apache.logging.log4j:log4j:$log4jVersion"

    testRuntimeOnly "org.jparsec:jparsec:$jparsecVersion"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled 'true' == jacoco_htmlReport
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/',
                'http://ical4j.github.io/docs/ical4j/api/4.0.0-beta6/'
    }
}

scmVersion {
    tag {
        prefix = 'ical4j-vcard-'
        branchPrefix = ['ical4j-vcard-1.x-maintenance': 'jdk8_ical4j_vcard']
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = ['master': 'simple']
    branchVersionCreator = ['ical4j-vcard-1.x-maintenance': 'simple']
    branchVersionCreator = ['develop': 'simple']
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

group = 'org.mnode.ical4j'
description = 'Support for the vCard specification'
version = scmVersion.version

jar {
    manifest {
        attributes(
                'Implementation-Title': 'iCal4j vCard',
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': 'Ben Fortuna'
        )
    }
}

revapi {
    oldVersion = revApiOldVersion
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

publishing {
    publications {
        ical4j_vcard(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-vcard')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-vcard.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-vcard.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = !isReleaseVersion ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
//        maven {
//            name = "GitHubPackages"
//            url = uri("https://maven.pkg.github.com/ical4j/ical4j")
//            credentials {
//                username = System.getenv("GPR_USERNAME")
//                password = System.getenv("GPR_TOKEN")
//            }
//        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.ical4j_vcard
}
